plugins {
    id "java-library"
    id "com.gtnewhorizons.retrofuturagradle" version "1.3.26"
    id "cl.franciscosolis.blossom-extended" version "1.3.1"
}

// Set the toolchain version to decouple the Java we run Gradle with from the Java used to compile and run the mod
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
        // Azul covers the most platforms for Java 8 toolchains, crucially including MacOS arm64
        vendor.set(org.gradle.jvm.toolchain.JvmVendorSpec.AZUL)
    }
}

minecraft {
    mcVersion.set(minecraft_version)
    username.set("Developer")

    injectedTags.put("MOD_VERSION", mod_version)
    injectedTags.put("MOD_NAME", "Sodium-Vintage")
}

tasks.injectTags.outputClassName.set("${mod_base_package}.Tags")

tasks.deobfuscateMergedJarToSrg.configure {accessTransformerFiles.from("src/main/resources/META-INF/sodium_at.cfg")}

repositories {
    maven {	url "https://maven.cleanroommc.com" }
    maven {
        name 'SpongePowered Maven'
        url 'https://repo.spongepowered.org/maven'
    }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    annotationProcessor "org.ow2.asm:asm-debug-all:5.2"
    annotationProcessor "com.google.guava:guava:32.1.2-jre"
    annotationProcessor "com.google.code.gson:gson:2.8.9"

    implementation("zone.rong:mixinbooter:8.9")

    def mixinBooter = modUtils.enableMixins("org.spongepowered:mixin:0.8.3", "sodium.mixins.refmap.json")
    api (mixinBooter) {
        transitive = false
    }
    annotationProcessor (mixinBooter) {
        transitive = false
    }

    implementation rfg.deobf("curse.maven:ae2-223794:2747063")
}

jar {
    manifest {
        attributes (
                "FMLCorePlugin": "me.jellysquid.mods.sodium.client.SodiumMixinTweaker",
                "FMLCorePluginContainsFMLMod": true,
                "ForceLoadAsMod": true,
                "FMLAT": "sodium_at.cfg"
        )
    }
}

processResources.configure {
    blossom {
        String mcmod = "src/main/resources/mcmod.info"
        String mcmeta = "src/main/resources/pack.mcmeta"
        replaceToken("mod_id", mod_id, mcmod)
        replaceToken("mod_name", mod_name, mcmod)
        replaceToken("mod_description", mod_description, mcmod)
        replaceToken("mod_version", mod_version, mcmod)
        replaceToken("mod_authors", mod_authors, mcmod)
        replaceToken("minecraft_version", minecraft_version, mcmod)
        replaceToken("mod_id", mod_id, mcmeta)
    }
}